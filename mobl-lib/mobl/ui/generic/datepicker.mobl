module mobl::ui::generic::datepicker

import mobl
import mobl::ui::generic

function getMonthName(m : Num) : String {
  var months : Array<String> = Array<String>("Jan", "Feb", "Mar", "Apr", "May", "Jun", "Jul", "Aug", "Sep", "Oct", "Nov", "Dec");
  return months.get(m);
}

style narrowNumFieldStyle {
  -webkit-appearance: none;
  -webkit-box-flex: 100;
  -webkit-rtl-ordering: logical;
  -webkit-user-select: text;
  -webkit-text-size-adjust: 140%;
  -moz-text-size-adjust: 140%;
  -webkit-appearance: textarea;
  background: #fff -webkit-gradient(linear, 0% 0%, 0% 100%, from(white),
    to(white) );
  border: 0;
  font-size: 1em;
  height: 1em;
  padding: 0;
  text-align: center;
  width: 3em;
}

control narrowNumField(n : Num, onchange : Callback = null) {
  <input type="text" databind=n onkeyup={
    if(!Math.isNaN(n)) {
      onchange(null);
    }
  } class=narrowNumFieldStyle/>
}

control datePicker(d : DateTime) {
  var visible = false
  var day = d.getDate()
  var year = d.getFullYear()
  label(d.toDateString(), onclick={
    visible = visible ? false : true;
  })
  <div>
  when(visible) {
    <div style="float: left; text-align: center;">
    button("+", onclick={
      d.setDate(d.getDate() + 1);
      day = d.getDate();
      d = d;
    })<br/>
    narrowNumField(day, onchange={ d.setDate(day); d=d; })<br/>
    button("-", onclick={
      d.setDate(d.getDate() - 1);
      d = d;
    })
    </div>
    <div style="float: left; text-align: center;">
    button("+", onclick={
      d.setMonth(d.getMonth() + 1);
      d = d;
    })<br/>
    label(getMonthName(d.getMonth()))<br/>
    button("-", onclick={
      d.setMonth(d.getMonth() - 1);
      d = d;
    })
    </div>
    <div style="float: left; text-align: center;">
    button("+", onclick={
      d.setFullYear(d.getFullYear() + 1);
      year = d.getFullYear();
      d = d;
    })<br/>
    narrowNumField(year, onchange={ d.setFullYear(year); d=d; })<br/>
    button("-", onclick={
      d.setFullYear(d.getFullYear() - 1);
      year = d.getFullYear();
      d = d;
    })
    </div>
  }
  <div style="clear: both"/>
  </div>
}